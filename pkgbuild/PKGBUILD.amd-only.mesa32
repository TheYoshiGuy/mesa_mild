# Maintainer: Lone_Wolf <lonewolf at xs4all dot nl>
# Contributor: Armin K. <krejzi at email dot com>
# Contributor: Kristian Klausen <klausenbusk@hotmail.com>
# Contributor: Egon Ashrafinia <e.ashrafinia@gmail.com>
# Contributor: Tavian Barnes <tavianator@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Antti "Tera" Oja <antti.bofh@gmail.com>
# Contributor: Diego Jose <diegoxter1006@gmail.com>

pkgbase=lib32-mesa-git
pkgname=('lib32-mesa-git' 'lib32-mesa-libgl-git')
pkgdesc="an open-source implementation of the OpenGL specification, git version"
pkgver=17.2.0_devel.92872.66c01409cc
pkgrel=1
arch=('x86_64')
makedepends=('python2-mako' 'lib32-libxml2' 'lib32-libx11' 'glproto' 'lib32-libdrm' 'dri2proto' 'dri3proto' 'presentproto' 
             'lib32-libxshmfence' 'lib32-libxxf86vm'  'lib32-libxdamage' 'gcc-multilib' 'lib32-libvdpau' 'lib32-wayland' 'lib32-libelf' 'lib32-llvm-svn'
              'git' 'lib32-libtxc_dxtn' 'lib32-libgcrypt' 'lib32-systemd'
              'mesa-git' 'lib32-llvm-libs-svn' )
url="http://mesa3d.sourceforge.net"
license=('custom')
source=('mesa::git+https://github.com/mikakev1/mesa_mild_compatibility.git') 
sha512sums=('SKIP')

pkgver() {
    cd mesa
    read -r _ver <VERSION
    echo ${_ver/-/_}.$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build () {
  export CFLAGS="-m32 $CFLAGS"
  export CXXFLAGS="-m32 $CXXFLAGS"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export LLVM_CONFIG=/usr/bin/llvm-config32

  cd mesa
  ./autogen.sh  --build=i686-pc-linux-gnu --host=i686-pc-linux-gnu --target=i686-pc-linux-gnu --libdir=/usr/lib32 --prefix=/usr --sysconfdir=/etc --with-dri-driverdir=/usr/lib32/xorg/modules/dri --with-gallium-drivers=radeonsi,swrast --with-dri-drivers="" --with-platforms=x11,drm,wayland,surfaceless --with-vulkan-drivers=radeon --enable-texture-float -enable-nine --enable-gbm --enable-glx-tls --with-va-libdir=/usr/lib32/dri --enable-vpdau --disable-xvmc --enable-gles2 --disable-gles1 --enable-lmsensors --enable-gallium-extra-hud  -with-gnu-ld  --with-clang-libdir=/usr/lib/ --disable-libunwind

# Used configure settings
#
# --prefix=PREFIX                   install architecture-independent files in PREFIX
# --sysconfdir=DIR                  read-only single-machine data 
#                                   [PREFIX/etc]
# --with-dri-driverdir=DIR          directory for the DRI drivers
#                                   [${libdir}/dri]
# --with-gallium-drivers[=DIRS...]  comma delimited Gallium drivers list, e.g. "i915,nouveau,r300,r600,radeonsi,freedreno,svga,swrast,vc4,virgl"
#                                   [default=r300,r600,svga,swrast]
# --with-dri-drivers[=DIRS...]      comma delimited classic DRI drivers list, e.g. "swrast,i965,radeon"
#                                   [default=auto]
# --with-egl-platforms[=DIRS...]    comma delimited native platforms libEGL supports, e.g. "x11,drm"
#                                   [default=auto]
# --with-vulkan-drivers[=DIRS...]   comma delimited Vulkan drivers list, e.g. "intel"
#                                   [default=no]
# --enable-texture-float            enable floating-point textures and renderbuffers 
#                                   [default=disabled]
# --enable-osmesa                   enable OSMesa library
#                                   [default=disabled]
# --enable-xa                       enable build of the XA X Acceleration API
#                                   [default=disabled]
# --enable-nine                     enable build of the nine Direct3D9 API
#                                   [default=no]
# --disable-xvmc                     enable xvmc library
#                                   [default=auto]
# --enable-vdpau                    enable vdpau library
#                                    [default=auto]
# --enable-omx                      enable OpenMAX library
#                                   [default=disabled]
# --enable-opencl                   enable OpenCL library
#                                   [default=disabled]
# --enable-opencl-icd               Build an OpenCL ICD library to be loaded by an ICD implementation
#                                   [default=disabled]
# --enable-glx-tls                  enable TLS support in GLX
#                                   [default=disabled]

  make

}


package_lib32-mesa-git () {
  pkgdesc="an open-source implementation of the OpenGL specification, git version"
  depends=('mesa-git' 'lib32-libdrm' 'lib32-wayland' 'lib32-libxxf86vm' 'lib32-libxdamage' 'lib32-libxshmfence' 'lib32-elfutils'
           'lib32-libtxc_dxtn' 'lib32-llvm-libs-svn' 'lib32-libunwind')
  optdepends=('opengl-man-pages: for the OpenGL API man pages')
  provides=('lib32-mesa' 'lib32-opencl-mesa' 'lib32-vulkan-intel' 'lib32-vulkan-radeon' 'lib32-libva-mesa-driver' 'lib32-mesa-vdpau')
  replaces=('lib32-mesa' 'lib32-opencl-mesa' 'lib32-vulkan-intel' 'lib32-vulkan-radeon' 'lib32-libva-mesa-driver' 'lib32-mesa-vdpau')
  conflicts=('lib32-mesa' 'lib32-opencl-mesa' 'lib32-vulkan-intel' 'lib32-vulkan-radeon' 'lib32-libva-mesa-driver' 'lib32-mesa-vdpau')

  cd mesa
  make DESTDIR="$pkgdir" install

  # remove files provided by mesa-git
  rm -rf "$pkgdir"/etc
  rm -rf "$pkgdir"/usr/include

  install -m755 -d ${pkgdir}/usr/lib32/mesa
  # move libgl/EGL/glesv*.so to not conflict with blobs ?
  mv -v "$pkgdir"/usr/lib32/libGL.so*    "$pkgdir"/usr/lib32/mesa/
  mv -v "$pkgdir"/usr/lib32/libEGL.so*   "$pkgdir"/usr/lib32/mesa/
  mv -v "$pkgdir"/usr/lib32/libGLES*.so* "$pkgdir"/usr/lib32/mesa/

  install -m755 -d "$pkgdir"/usr/share/licenses/$pkgbase
}

package_lib32-mesa-libgl-git () {
  pkgdesc="Mesa 3-D graphics library"
  depends=('lib32-mesa-git')
  provides=('lib32-mesa-libgl' 'lib32-libgl' 'lib32-libgles' 'lib32-libegl' 'lib32-opengl-provider')
  replaces=('lib32-mesa-libgl' 'lib32-libgles' 'lib32-libegl')
  conflicts=('lib32-mesa-libgl' 'lib32-libgles' 'lib32-libegl')

  install -m755 -d "$pkgdir"/usr/lib32
  ln -s /usr/lib32/mesa/libGL.so.1.2.0 "$pkgdir"/usr/lib32/libGL.so.1.2.0
  ln -s libGL.so.1.2.0               "$pkgdir"/usr/lib32/libGL.so.1
  ln -s libGL.so.1.2.0               "$pkgdir"/usr/lib32/libGL.so

  ln -s /usr/lib32/mesa/libEGL.so.1.0.0 "$pkgdir"/usr/lib32/libEGL.so.1.0.0
  ln -s libEGL.so.1.0.0               "$pkgdir"/usr/lib32/libEGL.so.1
  ln -s libEGL.so.1.0.0               "$pkgdir"/usr/lib32/libEGL.so

  ln -s /usr/lib32/mesa/libGLESv1_CM.so.1.1.0 "$pkgdir"/usr/lib32/libGLESv1_CM.so.1.1.0
  ln -s libGLESv1_CM.so.1.1.0               "$pkgdir"/usr/lib32/libGLESv1_CM.so.1
  ln -s libGLESv1_CM.so.1.1.0               "$pkgdir"/usr/lib32/libGLESv1_CM.so

  ln -s /usr/lib32/mesa/libGLESv2.so.2.0.0 "$pkgdir"/usr/lib32/libGLESv2.so.2.0.0
  ln -s libGLESv2.so.2.0.0               "$pkgdir"/usr/lib32/libGLESv2.so.2
  ln -s libGLESv2.so.2.0.0               "$pkgdir"/usr/lib32/libGLESv2.so

  install -m755 -d "$pkgdir"/usr/share/licenses/lib32-mesa-libgl-git
}
