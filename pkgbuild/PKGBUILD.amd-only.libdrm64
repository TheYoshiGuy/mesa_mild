# Maintainer : Lone_Wolf <lonewolf at xs4all dot nl>
# Contributor : Jan de Groot <jgc@archlinux.org>

pkgname=libdrm-git
_pkgname=libdrm
pkgver=2.4.81.r10.gb6f450e0
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services, master git version"
arch=(i686 x86_64)
license=('custom')
depends=('libpciaccess')
makedepends=('valgrind' 'xorg-util-macros' 'libxslt' 'docbook-xsl')
url="http://dri.freedesktop.org/"
provides=('libdrm')
replaces=('libdrm')
conflicts=('libdrm')

source=('libdrm::git://anongit.freedesktop.org/drm/libdrm')
sha512sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_pkgname}"
#    echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
    git describe --long | sed 's/^libdrm-//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd ${_pkgname}
  ./autogen.sh --prefix=/usr --with-gnu-ld --enable-manpages --disable-vc4 --disable-freedreno --disable-vmwgfx --disable-nouveau  --enable-udev --disable-intel --disable-cairo-tests
  make
}


package() {
  cd ${_pkgname}
  make DESTDIR="$pkgdir" install
  install -m755 -d "$pkgdir/usr/share/licenses/$pkgname"
}
